<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.10">
  <POU Name="InterfaceManualV1" Id="{69f8e896-e3c8-4e08-b157-7fa165a49498}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK InterfaceManualV1
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR

	comm AT%MB1024: commManual;
	
	monitorDevice : POINTER TO CommonBase;
	controlDevice : POINTER TO CommonBase;
	state : INT := 0;
	
	//need to explicit call write persistance data , to store into non-violate memory
	writePersistance : WritePersistentData;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF writePersistance.START THEN
	writePersistance.START := false;
END_IF

//monitor
CASE TO_DWORD(comm.monitorType) OF
	eCategrories.SELECTION_AXIS:
		monitorDevice := ADR(gvl.axisControls[comm.monitorIndex+1]); // index start from 1
	eCategrories.SELECTION_CYLINDER:
		monitorDevice := ADR(gvl.cylinderControls[comm.monitorIndex+1]);
	eCategrories.SELECTION_UNIT:
		monitorDevice := gvl.stations[comm.monitorIndex+1];
END_CASE
comm.monitorBlock := monitorDevice^.Monitor();
//control
CASE comm.commitSelection OF
	eCategrories.SELECTION_AXIS:
		controlDevice :=  ADR(gvl.axisControls[comm.commitIndex+1]);
	eCategrories.SELECTION_CYLINDER:
		controlDevice := ADR(gvl.cylinderControls[comm.commitIndex+1]);
	eCategrories.SELECTION_UNIT:
		controlDevice := gvl.stations[comm.commitIndex+1];
	eCategrories.SELECTION_COMMAND_BLOCK:
		//axis should be pre-selected
		controlDevice := ADR(gvl.axisControls[comm.commitBlock.command.base.axisId+1]);
END_CASE
CASE state OF
	0:
		IF comm.wordIn.0 AND comm.wordIn.1 THEN
			//ENGAGED_HMI and RUN
			state := 1;
		END_IF
	1:
		CASE comm.commitMode OF
			//Manual execution
			eModes.MODE_EXE_AXIS,eModes.MODE_EXE_COMMAND_BLOCK,eModes.MODE_EXE_CYLINDER,eModes.MODE_EXE_UNIT:
				comm.wordOut.1 := controlDevice^.Control(comm.commitBlock,comm.commitMode); //return whether done
			eModes.MODE_UPLOAD_DATA_BLOCK:
				comm.commitBlock := controlDevice^.ConfigureRead(comm.commitMode);
				comm.wordOut.1 := TRUE;
			eModes.MODE_DOWNLOAD_DATA_BLOCK:
				controlDevice^.ConfigureWrite(comm.commitBlock,comm.commitMode);
				writePersistance.START := TRUE;
				comm.wordOut.1 := TRUE;//done
		END_CASE
		
		//interrupt operation
		IF NOT comm.wordIn.0 THEN
			controlDevice^.Stop();
		END_IF
		
		IF comm.wordOut.1 OR NOT comm.wordIn.0 THEN
			//DONE or ENGAGED_HMI off
			state :=2;
		END_IF
	2:
		IF NOT comm.wordIn.1 THEN
			comm.wordOut.1 := FALSE;
			state := 0;
		END_IF
END_CASE

//Write-in non-violate memory
writePersistance(
		NETID:= , 
		PORT:= 851, 
		START:= , 
		TMOUT:= , 
		BUSY=> , 
		ERR=> , 
		ERRID=> );
]]></ST>
    </Implementation>
    <LineIds Name="InterfaceManualV1">
      <LineId Id="249" Count="2" />
      <LineId Id="248" Count="0" />
      <LineId Id="112" Count="18" />
      <LineId Id="213" Count="0" />
      <LineId Id="211" Count="0" />
      <LineId Id="132" Count="8" />
      <LineId Id="146" Count="0" />
      <LineId Id="141" Count="2" />
      <LineId Id="149" Count="0" />
      <LineId Id="162" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="246" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="145" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="218" Count="0" />
      <LineId Id="215" Count="2" />
      <LineId Id="193" Count="0" />
      <LineId Id="154" Count="1" />
      <LineId Id="157" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="151" Count="1" />
      <LineId Id="158" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="159" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="247" Count="0" />
      <LineId Id="237" Count="0" />
      <LineId Id="239" Count="6" />
      <LineId Id="238" Count="0" />
      <LineId Id="236" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>