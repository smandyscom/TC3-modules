<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.10">
  <POU Name="RunUnitCellularA" Id="{8c290897-f573-4530-b2b8-1a75c3433cf9}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK RunUnitCellularA EXTENDS RunUnitIndexerMaster
VAR_INPUT
	allHit : BOOL;
	allReach : BOOL;
	allRelease : BOOL;
END_VAR
VAR_OUTPUT
	triggerHit : BOOL;
	triggerRelease : BOOL;
END_VAR
VAR
	// conveyor related , Fast + Slow = pitch
	ptCommandFast : POINTER TO ParameterSet;
	ptCommandSlow : POINTER TO ParameterSet;
	
	//Checkpoints
	commandTriggerRaise: ParameterSet; //trigger point
	commandCheckRaise : ParameterSet; //if not all lifter done until then , slowdown conveyor motion
	//Lifters
	//lifters : ARRAY[1..3] OF POINTER TO RunLifterCelluar;
	commandSetZero : ParameterSet;
	commandFinalGoal : ParameterSet;
	ptCommandConpensate : POINTER TO ParameterSet;

	fastExecute : BOOL;
	slowExecute : BOOL;
	finalGoalExecute : BOOL;
	
	fastDone : BOOL;
	slowDone: BOOL;
	finalGoalDone : BOOL;
			
END_VAR
VAR CONSTANT
	alarmCodeJammed : UDINT := 16#101;
	alarmCodeNotReach : UDINT := 16#102;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//implement state 3
isLastSlotOccupied := TRUE; //always occupied , force to transmit to downstream

SUPER^(); //run indexer master

//Buffer modes
IF NOT fastDone AND fastExecute THEN
	fastDone := Axis^.ExecuteParameterBuffer(ptCommandFast,
	bufferIndex:=1);
END_IF
IF NOT slowDone AND slowExecute THEN
	slowDone := Axis^.ExecuteParameterBuffer(ptCommandSlow,
	bufferIndex:=2);
END_IF
IF NOT finalGoalDone AND finalGoalExecute THEN
	finalGoalDone := Axis^.ExecuteParameterBuffer(ADR(commandFinalGoal),
	bufferIndex:=3);
END_IF

IF bSerialTransition AND State = 1 THEN
	triggerRelease:=TRUE;
	NextState := 16#10; //state override : release lifters
END_IF

IF State >=3 AND State <=6 AND_THEN Upstream<>0 AND Upstream^.syncEngaged AND NOT Upstream^.syncAcknowledged THEN	
	Upstream^.syncAcknowledged := TRUE;//pull upstream when ready
	//Data transmit
	//SlotBase^.wordOut.0 := TRUE; //TODO , follow that status
	//SlotBase^.id := Upstream^.syncMatValue;
	//set-up final goal
	commandFinalGoal.command := eCommandTypes.POSITION;
	commandFinalGoal.parameter.position.Velocity := ptCommandFast^.parameter.position.Velocity;
	commandFinalGoal.parameter.position.Acceleration := ptCommandFast^.parameter.position.Acceleration;
	commandFinalGoal.parameter.position.Deceleration := ptCommandFast^.parameter.position.Deceleration;
	commandFinalGoal.parameter.position.Position := Axis^.PositionFeedback + ptCommandSlow^.parameter.position.Position + ptCommandConpensate^.parameter.position.Position; //goal extended
	
	//entered atomic operation , do not interrupt
	atomicOperation:=TRUE;
END_IF
//once alarm occured , release atomic 
IF atomicOperation AND AlarmCode<>0 THEN
	atomicOperation := FALSE; 
END_IF

CASE outputState OF
	//-------------------------release lifters
	16#10:
	commandSetZero.command := eCommandTypes.SET_POSITION;
	commandSetZero.parameter.position.Position := 0;
	
	IF NOT transitionByte.0 THEN
		transitionByte.0 := Axis^.ExecuteParameter(ptBlock:= ADR(commandSetZero));//reset pos command to zero
	END_IF
	
	IF allRelease AND transitionByte.0 THEN
		//reset all 	
		fastExecute := FALSE;
		slowExecute := FALSE;
		finalGoalExecute:=FALSE;

		fastDone := FALSE;
		slowDone := FALSE;
		finalGoalDone:=FALSE;	
	
		triggerRelease:=FALSE;
		
		//before enter deceleration
		commandCheckRaise.parameter.position.Position := (ptCommandFast^.parameter.position.Velocity * ptCommandFast^.parameter.position.Velocity);
		commandCheckRaise.parameter.position.Position := commandCheckRaise.parameter.position.Position / (ptCommandFast^.parameter.position.Deceleration * 1.8);
		
		NextState:=2; //merge back default sequence
	END_IF
	//------------------start running
	3:
	fastExecute := TRUE; //start to go
	bSerialTransition:= TRUE; //serial transition
	4:
	IF Axis^.PositionFeedback >= commandTriggerRaise.parameter.position.Position THEN		
		triggerHit := TRUE;//until trigger raise reached , trigger all lifter to move
		bSerialTransition:= TRUE; //serial transition
	//Else : on-going , not trigger lifter
	END_IF
	5:
	IF Axis^.PositionFeedback >= commandCheckRaise.parameter.position.Position THEN
		IF allHit THEN
			triggerHit:=false;
			slowExecute:=TRUE;//able to slowdown to bump
			syncEngaged := false ;// sending over
			bSerialTransition:= TRUE; //serial transition
		ELSIF fastDone THEN
			//fast had executed , but not all hit , trigger alarm (stop doing next move
			AlarmCode := alarmCodeJammed;
		END_IF
	//Else : on going , not reached the check point
	END_IF
	6:
	IF Upstream<>0 AND_THEN Upstream^.syncAcknowledged AND fastDone THEN
		//wait first command block done , otherwise would raise 0x4292 (buffer fulled)
		finalGoalExecute:=TRUE;//need do extension
		bSerialTransition:= TRUE; //serial transition	
	ELSIF allReach THEN
		//NextState := 16#20; //manually put carrier
	END_IF
	//---------------------------------------------
	7:
	// on extension , only one oppertunity
	bSerialTransition := finalGoalDone;
	//------------------------end of running
	8:
	IF allReach THEN
		bSerialTransition := TRUE;
	ELSE
		AlarmCode := alarmCodeNotReach;
	END_IF
	9:
	IF Upstream <> 0 AND_THEN NOT Upstream^.syncEngaged THEN
		Upstream^.syncAcknowledged := FALSE; // received carrier
		NextState :=1; //since working signal raised by individual lifter? 
		//TDOD , material transmission merge 
	ELSE
		// upstream not synchroized			
	END_IF
END_CASE
]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{96d07f02-073e-4434-a75c-09e5135212ba}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	
	argParent : POINTER TO RunUnitGeneric;
	argSlotBase : POINTER TO RunMaterial;
	argSlotLast : POINTER TO RunMaterial;
	
	argUpstream : POINTER TO RunUnitTransporterBase;
	argAxis : POINTER TO RunAxis;

END_VAR
VAR CONSTANT
	indexFast : INT := 1;
	indexSlow : INT := 2;
	indexComp : INT := 3;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//initialize 
Parent := argParent;
SlotBase := argSlotBase;
SlotLast := argSlotLast;

Axis := argAxis;
Upstream := argUpstream;

//
IF Axis<>0 THEN
	ptCommandFast := ADR(Axis^.ptpCommands[indexFast]);
	ptCommandSlow := ADR(Axis^.ptpCommands[indexSlow]);
	ptCommandConpensate := ADR(Axis^.ptpCommands[indexComp]);
	commandTriggerRaise.parameter.position.Position := 250; //carrier length
END_IF

IF Upstream=0 THEN
	isProxyMode:=TRUE; //no upstream , i am proxy
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="RunUnitCellularA">
      <LineId Id="1642" Count="0" />
      <LineId Id="1801" Count="0" />
      <LineId Id="1643" Count="1" />
      <LineId Id="1647" Count="13" />
      <LineId Id="1662" Count="17" />
      <LineId Id="1898" Count="0" />
      <LineId Id="1897" Count="0" />
      <LineId Id="1680" Count="0" />
      <LineId Id="1912" Count="0" />
      <LineId Id="1908" Count="2" />
      <LineId Id="1681" Count="22" />
      <LineId Id="1764" Count="0" />
      <LineId Id="1762" Count="0" />
      <LineId Id="1765" Count="0" />
      <LineId Id="1763" Count="0" />
      <LineId Id="1704" Count="7" />
      <LineId Id="1713" Count="11" />
      <LineId Id="1902" Count="1" />
      <LineId Id="1727" Count="4" />
      <LineId Id="1733" Count="2" />
      <LineId Id="1798" Count="1" />
      <LineId Id="1740" Count="0" />
      <LineId Id="1907" Count="0" />
      <LineId Id="1741" Count="1" />
      <LineId Id="1853" Count="0" />
      <LineId Id="1747" Count="1" />
      <LineId Id="1913" Count="0" />
      <LineId Id="1917" Count="0" />
      <LineId Id="1914" Count="0" />
      <LineId Id="1916" Count="0" />
      <LineId Id="1915" Count="0" />
      <LineId Id="1841" Count="0" />
      <LineId Id="1844" Count="0" />
      <LineId Id="1846" Count="0" />
      <LineId Id="1852" Count="0" />
      <LineId Id="1918" Count="0" />
      <LineId Id="1848" Count="0" />
      <LineId Id="1850" Count="0" />
      <LineId Id="1847" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="1863" Count="0" />
    </LineIds>
    <LineIds Name="RunUnitCellularA.FB_init">
      <LineId Id="15" Count="5" />
      <LineId Id="14" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="73" Count="2" />
      <LineId Id="32" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>