<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="RunPnueIndexer" Id="{0282178a-e4a8-43e2-88ce-00a99b488386}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK RunPnueIndexer EXTENDS RunUnitIndexerMaster
VAR_INPUT
	sensorPairs : ARRAY[0..3] OF defSensorPair; //define stations sensor flag signatures
	fixtures : helperOutputSelection;
END_VAR
VAR_OUTPUT
END_VAR
VAR
	//devices
	lockCylinder : POINTER TO RunCylinder;
	pistonCylinder : POINTER TO RunCylinder;
	
	
	
	//----------------
	//	Initialization
	//----------------
	revolveCounter : INT;
	tickTime  AT%Q* : DWORD; 
	temp : bool;
END_VAR
VAR PERSISTENT
	revolveGoal : INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^(); //run indexer master

tickTime := TIME_TO_DWORD(workingTimerCache);

CASE outputState OF
	//---------------------------
	//	Initialization : revolve n times
	//---------------------------	
	eSpecialStates.INITIAL_BEGIN:
		revolveCounter:=0;
		bSerialTransition := TRUE;
	eSpecialStates.INITIAL_STATE_1:
		bSerialTransition := lockCylinder^.Execute(eCylinderCommands.COMMAND_B);
	eSpecialStates.INITIAL_STATE_2:
		bSerialTransition := pistonCylinder^.Execute(eCylinderCommands.COMMAND_B);
	eSpecialStates.INITIAL_STATE_3:
		bSerialTransition := lockCylinder^.Execute(eCylinderCommands.COMMAND_A);
	eSpecialStates.INITIAL_STATE_4:
		bSerialTransition := pistonCylinder^.Execute(eCylinderCommands.COMMAND_A);
		IF bSerialTransition THEN
			revolveCounter := revolveCounter+1;
			IF revolveCounter < revolveGoal THEN
				NextState := eSpecialStates.INITIAL_STATE_1; //repeat
			ELSE
				NextState := eSpecialStates.INITIAL_STATE_5; //to lock
			END_IF	
		END_IF
	eSpecialStates.INITIAL_STATE_5:
		bSerialTransition := lockCylinder^.Execute(eCylinderCommands.COMMAND_B);
		IF bSerialTransition THEN
			
			FOR nCounter :=0 TO SlotCount-1 BY 1 DO
				SlotBase[nCounter].Valid := FALSE; //reset
			END_FOR
			
			NextState := eSpecialStates.INITIAL_END; //terminated
		END_IF
	//------------------start running
	// S16#10 - : setup vacuum fixtures
	// S16#20 - : pnuematic indexer movement
	// 1. unlock
	// 2. shift out
	// 3. lock
	//------------------
	16#10:
		//activate fixture by slot valid status
		FOR nCounter := 0 TO SlotCount-1 BY 1 DO
			fixtures.Trigger(sensorPairs[nCounter],SlotBase[nCounter].Valid); 
		END_FOR
		NextState := 16#20;
	//----------
	16#20:
		bSerialTransition := pistonCylinder^.Execute(eCylinderCommands.COMMAND_B) ;
	16#21:
		bSerialTransition := lockCylinder^.Execute(eCylinderCommands.COMMAND_A);
	16#22:
		bSerialTransition := pistonCylinder^.Execute(eCylinderCommands.COMMAND_A);
	16#23:
		bSerialTransition := lockCylinder^.Execute(eCylinderCommands.COMMAND_B);
		IF bSerialTransition THEN
			
			
		
			fixtures.ReleaseAll(); //release all fixture
			NextState := 16#0F; //join master sequence
		END_IF
END_CASE]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{8dfee478-70bf-4472-a4ae-21e0c723276a}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	
	argParent : POINTER TO RunUnitGeneric;
	argSlotBase : POINTER TO RunMaterial;
	argSlotLast : POINTER TO RunMaterial;
	
	argUpstream : POINTER TO RunUnitTransporterBase;
	argAxis : POINTER TO RunAxis;
	
	argLockCylinder : POINTER TO RunCylinder;
	argPistonCylinder : POINTER TO RunCylinder;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//initialize 
Parent := argParent;
SlotBase := argSlotBase;
SlotLast := argSlotLast;

Axis := argAxis;
Upstream := argUpstream;

isProxyMode:=FALSE; //no proxy


//calculate how much slot related
SlotCount :=  TO_INT((SlotLast-SlotBase)/SIZEOF(RunMaterial) + 1);

lockCylinder := argLockCylinder;
pistonCylinder := argPistonCylinder;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{2cdffb39-9857-4d32-a795-20f33e659f2a}">
      <Declaration><![CDATA[METHOD Initialize : BOOL
VAR_INPUT
END_VAR
VAR
	nCounter : int;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//once initializing method call , redirect current state to initalizing region
IF (TO_UINT(State) AND TO_UINT(eSpecialStates.INITIAL_REGION)) =0 THEN
	NextState := eSpecialStates.INITIAL_BEGIN; //not in intializing region , change
END_IF

Initialize := (State = eSpecialStates.INITIAL_END);

IF Initialize THEN
	//clear all remained signal
	FOR nCounter:=1 TO 32 BY 1 DO
		slaveWorkingSignal[nCounter] := FALSE;
	END_FOR
	//clear all internal status
	bEnStep := FALSE;
	bTrigStep := FALSE;
	bIsBypassed := FALSE;
	bWorkingOverride := FALSE;
	bMaterialOverride := FALSE;	
END_IF]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="RunPnueIndexer">
      <LineId Id="9" Count="0" />
      <LineId Id="235" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="103" Count="1" />
      <LineId Id="102" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="117" Count="2" />
      <LineId Id="105" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="107" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="122" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="129" Count="1" />
      <LineId Id="127" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="150" Count="2" />
      <LineId Id="181" Count="4" />
      <LineId Id="153" Count="1" />
      <LineId Id="19" Count="0" />
      <LineId Id="59" Count="1" />
      <LineId Id="22" Count="1" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="26" Count="4" />
      <LineId Id="109" Count="0" />
      <LineId Id="155" Count="1" />
      <LineId Id="31" Count="0" />
      <LineId Id="209" Count="2" />
      <LineId Id="46" Count="0" />
      <LineId Id="32" Count="1" />
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="RunPnueIndexer.FB_init">
      <LineId Id="34" Count="3" />
      <LineId Id="43" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="60" Count="1" />
      <LineId Id="76" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="91" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="95" Count="0" />
    </LineIds>
    <LineIds Name="RunPnueIndexer.Initialize">
      <LineId Id="18" Count="0" />
      <LineId Id="20" Count="2" />
      <LineId Id="14" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="39" Count="2" />
      <LineId Id="37" Count="0" />
      <LineId Id="43" Count="5" />
      <LineId Id="38" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>