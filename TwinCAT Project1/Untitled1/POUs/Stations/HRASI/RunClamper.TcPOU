<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="RunClamper" Id="{4cfbb8e7-dd19-45d2-8e96-6129406da078}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK RunClamper EXTENDS RunUnitSlave
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	//devices
	horizon : POINTER TO RunCylinder;
	clamper : POINTER TO RunCylinder;
	//
	configSnap : BOOL; //set true for instance of Snap
	
	//
	tickTime AT%Q*: DWORD ; //in ms
	commIdTrigger AT%Q* : DWORD;
END_VAR
VAR CONSTANT
	bScaraReady : INT := 1;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^();

//Link to :
// 1. Indexer - working signal
// 2. Bridge - working signal dispatch  to SCARA

tickTime := TIME_TO_DWORD(workingTimerCache);

CASE outputState OF
	//---------------------------
	//	Initialization : hold and push
	//---------------------------	
	eSpecialStates.INITIAL_BEGIN:
		clamper^.isRemoteControl := FALSE; //off remote control
		bSerialTransition := clamper^.Execute(eCylinderCommands.NO_COMMAND); //remain
	eSpecialStates.INITIAL_STATE_1:
		bSerialTransition := horizon^.Execute(eCylinderCommands.COMMAND_B);
	eSpecialStates.INITIAL_STATE_2:
		bSerialTransition := clamper^.Execute(eCylinderCommands.COMMAND_A); //release
	eSpecialStates.INITIAL_STATE_3:
		bSerialTransition := horizon^.Execute(eCylinderCommands.COMMAND_A); //back
	eSpecialStates.INITIAL_STATE_4:
		bSerialTransition := TRUE;
	eSpecialStates.INITIAL_STATE_5:
		bSerialTransition := TRUE;
		IF bSerialTransition THEN
			NextState := eSpecialStates.INITIAL_END;
		END_IF
	//----------------------------
	//	Working
	//----------------------------
	eSpecialStates.SLAVE_ENTER_PROCEDURE:
		IF bMaterialOverride THEN
			bSerialTransition := TRUE;
		ELSE
			NextState := 16#207; //bypass
		END_IF
	16#201:
		//Indexer in position , horizonin
		bSerialTransition := horizon^.Execute(eCylinderCommands.COMMAND_B);
	16#202:
		bSerialTransition := clamper^.Execute(eCylinderCommands.COMMAND_B);
	16#203:
		transitionByte.0 := horizon^.Execute(eCylinderCommands.COMMAND_A);
		transitionByte.1 := TRUE;
		
		IF transitionByte = 16#3 THEN
			
			slaveWorkingSignal[bScaraReady]:= TRUE; //trigger SCARA agent
			
			IF configSnap THEN
				clamper^.isRemoteControl := TRUE; // gave scara remote control
			END_IF
			
			bSerialTransition := TRUE;
		END_IF
	16#204:
		//Wait SCARA 
		bSerialTransition := NOT slaveWorkingSignal[bScaraReady];
		IF bSerialTransition AND previousNode<>0 AND_THEN previousNode^.isPurged  THEN
			isPurged := TRUE; //no further part would coming
		END_IF
		IF bSerialTransition THEN
			clamper^.isRemoteControl := FALSE; // demolish remote control
			commIdTrigger := SlotBase^.Id; //trigger the data logger
		END_IF
	16#205:
		//send part back to indexer
		bSerialTransition := clamper^.Execute(eCylinderCommands.COMMAND_B);
	16#206:
		//send part back to indexer
		bSerialTransition := horizon^.Execute(eCylinderCommands.COMMAND_B);
	16#207:
		//release part 
		bSerialTransition := clamper^.Execute(eCylinderCommands.COMMAND_A); //release
	16#208:
		//empty clamper extract
		IF horizon^.Execute(eCylinderCommands.COMMAND_A) THEN
			NextState := eSpecialStates.SLAVE_WORK_DONE;
		END_IF
	
	//-------------
	//	Last station should treated as empty
	//-------------
	eSpecialStates.SLAVE_WORK_DONE:
		// auto reset last slot
		IF configSnap THEN
			SlotBase^.Valid := FALSE;
		END_IF
		
		//bypass purge status
		IF NOT SlotBase^.Valid AND previousNode<>0 AND_THEN previousNode^.isPurged THEN
			isPurged := TRUE;
		END_IF
		
END_CASE]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{37c62737-651a-4f40-81e3-f79b76fcc99a}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	
	argParent : POINTER TO RunUnitGeneric;
	argSlotBase : POINTER TO RunMaterial;
	argSlotLast : POINTER TO RunMaterial;
	
	argWorkingSignal : POINTER TO BOOL;
	
	argHorizon : POINTER TO RunCylinder;
	argClamper : POINTER TO RunCylinder;
	argConfigSnap : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//initialize 
Parent := argParent;
SlotBase := argSlotBase;
SlotLast := argSlotLast;
//
IF bInitRetains THEN
	NextState := eSpecialStates.INITIAL_BEGIN;
END_IF

workingSignal := argWorkingSignal; //link

//device link
horizon := argHorizon;
clamper := argClamper;
configSnap := argConfigSnap;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="RunClamper">
      <LineId Id="110" Count="0" />
      <LineId Id="201" Count="0" />
      <LineId Id="111" Count="0" />
      <LineId Id="202" Count="1" />
      <LineId Id="385" Count="1" />
      <LineId Id="112" Count="1" />
      <LineId Id="195" Count="2" />
      <LineId Id="194" Count="0" />
      <LineId Id="405" Count="0" />
      <LineId Id="204" Count="13" />
      <LineId Id="198" Count="2" />
      <LineId Id="114" Count="0" />
      <LineId Id="170" Count="1" />
      <LineId Id="173" Count="1" />
      <LineId Id="172" Count="0" />
      <LineId Id="169" Count="0" />
      <LineId Id="115" Count="5" />
      <LineId Id="323" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="489" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="340" Count="1" />
      <LineId Id="343" Count="1" />
      <LineId Id="342" Count="0" />
      <LineId Id="132" Count="4" />
      <LineId Id="280" Count="2" />
      <LineId Id="345" Count="1" />
      <LineId Id="468" Count="0" />
      <LineId Id="347" Count="0" />
      <LineId Id="364" Count="2" />
      <LineId Id="137" Count="4" />
      <LineId Id="176" Count="0" />
      <LineId Id="146" Count="4" />
      <LineId Id="255" Count="4" />
      <LineId Id="261" Count="2" />
      <LineId Id="260" Count="0" />
      <LineId Id="299" Count="4" />
      <LineId Id="445" Count="0" />
      <LineId Id="29" Count="0" />
    </LineIds>
    <LineIds Name="RunClamper.FB_init">
      <LineId Id="16" Count="6" />
      <LineId Id="14" Count="1" />
      <LineId Id="11" Count="0" />
      <LineId Id="40" Count="1" />
      <LineId Id="39" Count="0" />
      <LineId Id="42" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>